allprojects {
    apply plugin: "idea"
    apply plugin: 'java'
    apply plugin: 'eclipse'
    apply plugin: 'jacoco'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    group = "cern.molr"

    repositories {
        mavenCentral()
        maven { url "https://repo.spring.io/snapshot" }
        maven { url "https://repo.spring.io/milestone" }
    }

    configurations.all {
        /*This dependency is automatically required by slf4j, it should be excluded to avoid infinite loop between
         slf4j and log4j
         */
        exclude group: "org.apache.logging.log4j", module: "log4j-to-slf4j"
        exclude group: "ch.qos.logback", module: "logback-classic"
        exclude group: "ch.qos.logback", module: "logback-core"
    }

    dependencies {
        compile group: 'org.slf4j', name: 'slf4j-api', version: slf4jVersion
        compile group: 'org.apache.logging.log4j', name: 'log4j-core', version: log4jVersion
        compile group: 'org.apache.logging.log4j', name: 'log4j-api', version: log4jVersion
        compile group: 'org.apache.logging.log4j', name: 'log4j-slf4j-impl', version: log4jVersion


        compile group: 'org.springframework', name: 'spring-webflux', version: springFrameworkVersion
        compile group: 'org.springframework', name:'spring-websocket', version: springFrameworkVersion

        compile group: 'org.springframework.boot', name: 'spring-boot-starter-integration', version: springBootVersion
        compile group: 'org.springframework.boot', name: 'spring-boot-starter-webflux', version: springBootVersion

        compile group: 'io.projectreactor.ipc', name: 'reactor-netty', version: reactiveNettyVersion
        compile group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: fasterXmlJacksonVersion

        testCompile group: 'junit', name: 'junit', version: jUnitVersion

    }

    tasks.withType(JavaCompile) {
        options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
    }

    jacocoTestReport {
        reports {
            xml.enabled true
            xml.destination new File("${buildDir}/reports/jacoco/report.xml")
            html.enabled false
            csv.enabled false
        }
    }
}

project(":api") {}

project(":commons") {
    dependencies {
        compile project(":api")
    }
}
project(":sample") {
    dependencies {
        compile project(":api")
        compile project(":commons")
    }
}
project(":supervisor") {
    dependencies {
        compile project(":sample")
    }
}
project(":server") {
    dependencies {
        compile project(":supervisor")
        compile project(":sample")
    }
}

project(":client") {
    dependencies {
        compile project(":server")
        compile project(":sample")
    }
}

